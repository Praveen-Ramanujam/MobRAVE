if( NOT WIN32 AND OPENRAVE_BIN_SUFFIX )
  set(OPENRAVEPY_VER_NAME "_openravepy_${OPENRAVE_VERSION_MAJOR}_${OPENRAVE_VERSION_MINOR}")
else()
  set(OPENRAVEPY_VER_NAME "_openravepy_")
endif()

set(OPENRAVEPY_VER_INSTALL_DIR "${OPENRAVEPY_INSTALL_DIR}/${OPENRAVEPY_VER_NAME}")
add_subdirectory(bindings)

if( OPT_BUILD_PACKAGE_DEFAULT )
  set(OPENRAVEPY_EGG "${CMAKE_CURRENT_BINARY_DIR}/openravepy.egg-info")
  configure_file("${CMAKE_CURRENT_SOURCE_DIR}/openravepy.egg-info.in" "${OPENRAVEPY_EGG}" IMMEDIATE @ONLY)
  install(FILES "${OPENRAVEPY_EGG}" DESTINATION ${OPENRAVE_PYTHON_INSTALL_DIR}  COMPONENT openrave-python-minimal)
  
  set(OPENRAVEPY_INIT "${CMAKE_CURRENT_BINARY_DIR}/__init__.py")
  configure_file("${CMAKE_CURRENT_SOURCE_DIR}/__init__.py.in" "${OPENRAVEPY_INIT}" IMMEDIATE @ONLY)
  install(FILES ${OPENRAVEPY_INIT} DESTINATION ${OPENRAVEPY_INSTALL_DIR}  COMPONENT openrave-python-minimal)
  set(CPACK_COMPONENT_OPENRAVE-PYTHON-MINIMAL_DISPLAY_NAME "Python ${PYTHON_MAJORMINOR_VERSION} bindings minimal subset" PARENT_SCOPE)
  set(CPACK_COMPONENT_OPENRAVE-PYTHON-MINIMAL_DESCRIPTION "Uninstall this package to remove all python bindings of all installations" PARENT_SCOPE)
  set(CPACK_COMPONENT_OPENRAVE-PYTHON-MINIMAL_DEPENDS openrave-minimal PARENT_SCOPE)
endif()

file(READ "${CMAKE_CURRENT_SOURCE_DIR}/../src/cppexamples/FindOpenRAVE.cmake" FindOpenRAVE)

# python executables need renaming
configure_file("${CMAKE_CURRENT_SOURCE_DIR}/openrave.py.in" "${CMAKE_CURRENT_BINARY_DIR}/openrave${OPENRAVE_BIN_SUFFIX}.py" IMMEDIATE @ONLY)
configure_file("${CMAKE_CURRENT_SOURCE_DIR}/openrave-robot.py.in" "${CMAKE_CURRENT_BINARY_DIR}/openrave${OPENRAVE_BIN_SUFFIX}-robot.py" IMMEDIATE @ONLY)
configure_file("${CMAKE_CURRENT_SOURCE_DIR}/openrave-createplugin.py.in" "${CMAKE_CURRENT_BINARY_DIR}/openrave${OPENRAVE_BIN_SUFFIX}-createplugin.py" IMMEDIATE @ONLY)

install(FILES "${CMAKE_CURRENT_BINARY_DIR}/openrave${OPENRAVE_BIN_SUFFIX}.py" "${CMAKE_CURRENT_BINARY_DIR}/openrave${OPENRAVE_BIN_SUFFIX}-robot.py" "${CMAKE_CURRENT_BINARY_DIR}/openrave${OPENRAVE_BIN_SUFFIX}-createplugin.py" DESTINATION bin PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ WORLD_EXECUTE WORLD_READ COMPONENT ${COMPONENT_PREFIX}python)

# install rest python files
install(FILES metaclass.py openravepy_ext.py misc.py pyANN.py DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR} COMPONENT ${COMPONENT_PREFIX}python)
install(FILES openravepy.__init__.py DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR} COMPONENT ${COMPONENT_PREFIX}python RENAME __init__.py)
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/examples" DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR} FILE_PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ WORLD_EXECUTE WORLD_READ COMPONENT ${COMPONENT_PREFIX}python PATTERN ".svn" EXCLUDE PATTERN ".pyc" EXCLUDE)
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/interfaces" DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR}  FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ COMPONENT ${COMPONENT_PREFIX}python PATTERN ".svn" EXCLUDE PATTERN ".pyc" EXCLUDE)
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/databases" DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR}  FILE_PERMISSIONS OWNER_WRITE OWNER_READ GROUP_READ WORLD_READ COMPONENT ${COMPONENT_PREFIX}python PATTERN ".svn" EXCLUDE PATTERN ".pyc" EXCLUDE)

set(CPACK_COMPONENT_${COMPONENT_PREFIX_UPPER}PYTHON_DISPLAY_NAME "Python ${PYTHON_MAJORMINOR_VERSION} bindings" PARENT_SCOPE)
set(CPACK_COMPONENT_${COMPONENT_PREFIX_UPPER}PYTHON_DEPENDS ${COMPONENT_PREFIX}base python-numpy openrave-python-minimal PARENT_SCOPE)

if( OPT_BUILD_PACKAGE_DEFAULT AND OPENRAVE_BIN_SUFFIX )
  install(CODE "execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory \${CMAKE_INSTALL_PREFIX}/${OPENRAVEPY_INSTALL_DIR})" COMPONENT openrave)
  install(CODE "execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink openrave${OPENRAVE_BIN_SUFFIX}.py openrave.py WORKING_DIRECTORY \${CMAKE_INSTALL_PREFIX}/bin)" COMPONENT openrave)
  install(CODE "execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink openrave${OPENRAVE_BIN_SUFFIX}-robot.py openrave-robot.py WORKING_DIRECTORY \${CMAKE_INSTALL_PREFIX}/bin)" COMPONENT openrave)
  install(CODE "execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink openrave${OPENRAVE_BIN_SUFFIX}-createplugin.py openrave-createplugin.py WORKING_DIRECTORY \${CMAKE_INSTALL_PREFIX}/bin)" COMPONENT openrave)
  install(CODE "execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink ${OPENRAVEPY_VER_NAME} _openravepy_ WORKING_DIRECTORY \${CMAKE_INSTALL_PREFIX}/${OPENRAVEPY_INSTALL_DIR})" COMPONENT openrave)
endif()

# ikfast component
install(FILES ikfast.py DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR} PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ GROUP_EXECUTE GROUP_READ WORLD_EXECUTE WORLD_READ COMPONENT ${COMPONENT_PREFIX}ikfast)
install(FILES ikfast_generator_cpp.py DESTINATION ${OPENRAVEPY_VER_INSTALL_DIR} COMPONENT ${COMPONENT_PREFIX}ikfast)
if( NOT OPENRAVE_USE_LOCAL_SYMPY )
  set(IKFAST_DEPENDS python-sympy python-mpmath)
else()
  set(IKFAST_DEPENDS)
endif()
if( CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX )
  set(IKFAST_DEPENDS ${IKFAST_DEPENDS} g++)
endif()
set(CPACK_COMPONENT_${COMPONENT_PREFIX_UPPER}IKFAST_DEPENDS ${COMPONENT_PREFIX}python ${IKFAST_DEPENDS} PARENT_SCOPE)
set(CPACK_COMPONENT_${COMPONENT_PREFIX_UPPER}IKFAST_DISPLAY_NAME "Robot Kinematics Compiler" PARENT_SCOPE)

set(CPACK_COMPONENTS_ALL ${CPACK_COMPONENTS_ALL} ${COMPONENT_PREFIX}python ${COMPONENT_PREFIX}ikfast PARENT_SCOPE)
